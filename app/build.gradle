apply plugin: 'com.android.application'

android {
    compileSdkVersion gradle.compileVersion
    buildToolsVersion gradle.buildVersion

    defaultConfig {
        applicationId gradle.pkgName
        minSdkVersion gradle.minVersion
        targetSdkVersion gradle.targetVersion
        versionCode gradle.verCode
        versionName gradle.verName
        multiDexEnabled true
    }

    signingConfigs {
        release {
            def Console console = System.console()
            //noinspection GroovyAssignabilityCheck
            storeFile file('../keystore/JiongBull.jks')
            storePassword console != null ? console.readLine("\nKeystore password: ") : ''
            keyAlias 'android'
            keyPassword console != null ? console.readLine("\nKey password: ") : ''
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "repository_v${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    sourceSets {
        main {
            manifest.srcFile '/src/main/AndroidManifest.xml'
            java.srcDirs = ['/src/main/java']
            aidl.srcDirs = ['/src/main/aidl']
            jniLibs.srcDirs = ['jniLibs']
            assets.srcDirs = ['/src/main/assets']
            res.srcDirs = ['/src/main/res']
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        dev {
            applicationId "${gradle.pkgName}.dev" // 测试环境
        }
        production {} // 生产环境
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:${gradle.supportVersion}"
    compile "com.android.support:design:${gradle.supportVersion}"
    compile "com.android.support:recyclerview-v7:${gradle.supportVersion}"
    compile "com.android.support:cardview-v7:${gradle.supportVersion}"
    compile 'com.makeramen:roundedimageview:2.2.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jiongbull:jlog:1.0.2'
//    compile 'com.squareup.okhttp:okhttp:2.7.0'
//    compile 'com.facebook.stetho:stetho:1.2.0'
//    compile 'com.facebook.stetho:stetho-okhttp:1.2.0'
}
